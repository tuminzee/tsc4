{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

int tlen(tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
    tuple result = empty_tuple();

    (int i, int j, int k) = (0, 0, 0);
    (int n, int m, int p) = (tlen(matrixA), tlen(matrixB), tlen(matrixB.at(0)));

    repeat (n) {
        tuple row = empty_tuple();

        repeat (p) {
            int s = 0;
            repeat (m) {
                s += matrixA.at(i).at(k) * matrixB.at(k).at(j);
                k += 1;
            }

            k = 0;
            row~tpush(s);

            j += 1;
        }

        j = 0;
        result~tpush(row);

        i += 1;
    }

    return result;
}